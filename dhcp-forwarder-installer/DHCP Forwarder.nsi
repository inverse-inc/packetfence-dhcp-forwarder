; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "DHCP Forwarder"
!define PRODUCT_VERSION "1.3"
!define PRODUCT_PUBLISHER "Inverse Inc."
!define PRODUCT_WEB_SITE "http://inverse.ca"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

; MUI 1.67 compatible ------
!include "MUI.nsh"
; Registry
!include x64.nsh

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Language Selection Dialog Settings
!define MUI_LANGDLL_REGISTRY_ROOT "${PRODUCT_UNINST_ROOT_KEY}"
!define MUI_LANGDLL_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
!define MUI_LANGDLL_REGISTRY_VALUENAME "NSIS:Language"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"
!insertmacro MUI_LANGUAGE "French"

!define CSIDL_COMMONAPPDATA '0x23'

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "DHCP Forwarder Installer.exe"
InstallDir "$PROGRAMFILES\DHCP Forwarder"
ShowInstDetails show
ShowUnInstDetails show

Section -SETTINGS
  SetOutPath "$INSTDIR"
  SetOverwrite ifnewer
SectionEnd

Function .onInit
  !insertmacro MUI_LANGDLL_DISPLAY
  Var /GLOBAL AppDataPath
  System::Call 'shell32::SHGetSpecialFolderPath(i $HWNDPARENT, t .r1, i ${CSIDL_COMMONAPPDATA}, i0)i.r0'
  StrCpy $AppDataPath "$1"
FunctionEnd

Section "Principal" SEC01
  SetOutPath "$INSTDIR"
  SetOverwrite ifnewer
  File "dhcp-forwarder-config-generator.exe"
  File "dhcp-forwarder.exe"
  File "nssm.exe"
SectionEnd

Section "Configurator" SEC02
	MessageBox MB_OK \
    "dhcp-forwarder-config-generator.exe will be launched from the install directory. Please answer throughfully as this configuration will be used to start the service at next step."
  ExecWait "$INSTDIR\dhcp-forwarder-config-generator.exe"
SectionEnd

Section "Service Installation" SEC03
	nsExec::Exec '$INSTDIR\nssm.exe stop DHCP-Forwarder'
	nsExec::Exec '$INSTDIR\nssm.exe remove DHCP-Forwarder confirm'
	MessageBox MB_OK \
    "DHCP Forwarder will be installed as a service with previously generated configuration."
	nsExec::ExecToStack '$INSTDIR\nssm.exe install DHCP-Forwarder "$INSTDIR\dhcp-forwarder.exe"'
	Pop $0
	Pop $1
	${If} $0 != 0
		DetailPrint "Return code of nssm was $0"
		Abort "There was a problem installing the service with nssm. It outputted: $1"
	${EndIf}	
SectionEnd

Section "Service Startup" SEC04
	MessageBox MB_OK \
    "DHCP Forwarder service will be started with previously generated configuration."
	nsExec::ExecToStack '$INSTDIR\nssm.exe start DHCP-Forwarder'
	Pop $0
	Pop $1
	${If} $0 == 1
		DetailPrint "Return code of nssm was $0"
		DetailPrint "Cancel and rerun this installer. Make sure the selected Interface is Connected and not a Wireless device prior to running this installer."
		Abort "There was a problem starting the service with nssm. It outputted: $1"
	${EndIf}
	${If} $0 != 0
		DetailPrint "Return code of nssm was $0"
		Abort "There was a problem starting the service with nssm. It outputted: $1"
	${EndIf}
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was unistalled with success."
FunctionEnd

Function un.onInit
  Var /GLOBAL AppDataPath2
  System::Call 'shell32::SHGetSpecialFolderPath(i $HWNDPARENT, t .r1, i ${CSIDL_COMMONAPPDATA}, i0)i.r0'
  StrCpy $AppDataPath2 "$1"
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to uninstall $(^Name) and all its components ?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  nsExec::Exec '$INSTDIR\nssm.exe stop DHCP-Forwarder'
  nsExec::Exec '$INSTDIR\nssm.exe remove DHCP-Forwarder confirm'
  Delete "$INSTDIR\dhcp-forwarder-config-generator.exe"
  Delete "$INSTDIR\nssm.exe"
  Delete "$INSTDIR\dhcp-forwarder.exe"
  Delete "$INSTDIR\DHCP-Forwarder.toml"
  SetAutoClose true
SectionEnd
